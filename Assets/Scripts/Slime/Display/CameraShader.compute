// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Render

// Texture3D<float> ColorMap;
Texture3D<half> TrailMap;

float4x4 cameraToWorld;
float4x4 cameraInverseProjection;
float4 color;

int resolution;

RWTexture2D<float4> Result;

struct Ray
{
    float3 origin;
    float3 direction;
};

bool IntersectBox(float3 ro, float3 rd, out float tmin, out float tmax)
{
    float3 invDir = 1.0f / rd;

    float3 t0 = (-0.5f - ro) * invDir;
    float3 t1 = (0.5f - ro) * invDir;

    float3 tmin3 = min(t0, t1);
    float3 tmax3 = max(t0, t1);

    tmin = max(max(tmin3.x, tmin3.y), tmin3.z);
    tmax  = min(min(tmax3.x, tmax3.y), tmax3.z);

    return tmax >= max(tmin, 0.0f);
}

float3 Trace(Ray ray)
{
    float tin, tout;
    if (!IntersectBox(ray.origin, ray.direction, tin, tout))
        return float3(0,0,0);

    tin = max(0.0f, tin);
    if (tin >= tout) return float3(0.0f,0.0f,0.0f);

    float3 dims = float3(resolution, resolution, resolution);
    float stepSize = 0.5 / resolution;
    // float stepSize = 0.01f;

    float3 startPos = ray.origin + ray.direction * (tin + 1e-4);
    float3 posVoxel = (startPos + 0.5f) * dims;
    float3 stepVecVoxel = (ray.direction * stepSize) * dims;
    int maxSteps = int((tout - tin) / stepSize);
    
    half trans = 1.0f;

    [loop]
    for(int i = 0; i < maxSteps; i++)
    {
        half alpha = TrailMap[int3(posVoxel)];
        trans *= (1.0h - alpha);
        if (trans < 0.02h) break;
        posVoxel += stepVecVoxel;
    }
    return color.rgb * (1.0h - trans);
}

Ray CreateCameraRay(float2 uv)
{
    float3 origin = mul(cameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    float3 direction = mul(cameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(cameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

[numthreads(8,8,1)]
void Render (uint3 id : SV_DispatchThreadID)
{
    uint w, h;
    Result.GetDimensions(w, h);

    float2 uv = (id.xy + 0.5f);
    uv.x /= w;
    uv.y /= h;
    uv = uv * 2.0f - 1.0f;

    Ray ray = CreateCameraRay(uv);
    float3 result = Trace(ray);
    Result[id.xy] = float4(result, 1.0f);
}
