// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

Texture3D<float> TrailMap;
SamplerState samplerTrailMap;

float4x4 cameraToWorld;
float4x4 cameraInverseProjection;
float opacity;

// float4 color;
float3 frontColor = float3(1.0, 0.4, 0.1); // Bright Orange
float3 backColor  = float3(0.1, 0.1, 1.0); // Deep Blue

int width;
int height;
int depth;

RWTexture2D<float4> Result;

struct Ray
{
    float3 origin;
    float3 direction;
};

bool IntersectBox(float3 ro, float3 rd, out float tmin, out float tmax)
{
    float3 invDir = 1.0f / rd;

    float3 t0 = (-0.5f - ro) * invDir;
    float3 t1 = (0.5f - ro) * invDir;

    float3 tmin3 = min(t0, t1);
    float3 tmax3 = max(t0, t1);

    tmin = max(max(tmin3.x, tmin3.y), tmin3.z);
    tmax  = min(min(tmax3.x, tmax3.y), tmax3.z);

    return tmax >= max(tmin, 0.0f);
}

float3 GetDepthColor(float t)
{
    return lerp(frontColor, backColor, t);
}

float3 Trace(Ray ray)
{
    float tin, tout;
    if (!IntersectBox(ray.origin, ray.direction, tin, tout))
        return float3(0,0,0);

    tin = max(0.0f, tin);
    if (tin >= tout) return float3(0.0f,0.0f,0.0f);

    float3 dims = float3(width, height, depth);
    float stepSize = 0.5 / width;
    // float stepSize = 0.01f;

    float3 startPos = ray.origin + ray.direction * (tin + 1e-4);
    float3 pos = (startPos + 0.5f);
    float3 stepVec = (ray.direction * stepSize);
    int maxSteps = int((tout - tin) / stepSize);
    
    // float trans = 1.0f;

    float3 accColor = 0.0;
    float accAlpha = 0.0;
    float t = 0;

    [loop]
    for(int i = 0; i < maxSteps; i++)
    {
        if (any(pos < 0) || any(pos > 1)) break;
        float density = TrailMap.SampleLevel(samplerTrailMap, pos, 0);

        // if (density > 0.1)
        // {
        //     accColor = 1.0f;
        //     break;
        // }
        float a = density * opacity; 
        float3 col = GetDepthColor(pos.z);
        accColor += col * a * (1.0 - accAlpha);
        accAlpha += a * (1.0 - accAlpha);
        
        if (accAlpha >= 0.98) break;

        // t += stepSize;
        pos += stepVec;
    }
    return accColor;
}

Ray CreateCameraRay(float2 uv)
{
    float3 origin = mul(cameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    float3 direction = mul(cameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(cameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint w, h;
    Result.GetDimensions(w, h);

    float2 uv = (id.xy + 0.5f);
    uv.x /= w;
    uv.y /= h;
    uv = uv * 2.0f - 1.0f;

    Ray ray = CreateCameraRay(uv);
    float3 result = Trace(ray);
    Result[id.xy] = float4(result, 1.0f);
}
